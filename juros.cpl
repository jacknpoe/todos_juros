#!/usr/bin/env cpl

! Cálculo dos juros, sendo que precisa de parcelas para isso
! Versão 0.1: 23/01/2025: versão feita sem muito conhecimento de CPL

DEFAULTFORMAT 1.15  ! para mostrar quinze casas depois da vírgula

! variáveis globais para simplificar as chamadas
INTEGER quantidade = 3
BOOLEAN composto = TRUE
REAL periodo = 30.0
REAL pagamentos(1..3)
REAL pesos(1..3)

! inicalização das matrizes pagamentos() e pesos()
LOOP FOR indice=1 TO 3
    pagamentos(indice) = 30.0*indice
    pesos(indice) = 1.0
REPEAT LOOP

! calcula a somatória de pesos()
REAL FUNCTION getPesoTotal()
    REAL acumulador = 0.0
    LOOP FOR indice = 1 TO quantidade
        acumulador = acumulador+pesos(indice)
    REPEAT LOOP
    RETURN acumulador
END getPesoTotal

! calcula o acréscimo a partir dos juros e parcelas
REAL FUNCTION jurosParaAcrescimo(REAL juros)
    REAL pesoTotal = getPesoTotal()
    IF juros <= 0.0 OR quantidade < 1 OR periodo <= 0.0 OR pesoTotal <= 0.0 THEN
        RETURN 0.0
    END IF

    REAL acumulador = 0.0

    LOOP FOR indice = 1 TO quantidade
        IF composto THEN
            acumulador = acumulador+pesos(indice)/(1.0+juros/100.0)^(pagamentos(indice)/periodo)
        ELSE
            acumulador = acumulador+pesos(indice)/(1.0+juros/100.0*pagamentos(indice)/periodo)
        END IF
    REPEAT LOOP

    IF acumulador <= 0.0 THEN
        RETURN 0.0
    END IF
    RETURN (pesoTotal/acumulador-1.0)*100.0
END jurosParaAcrescimo

! calcula os jurosw a partir do acréscimo e parcelas
REAL FUNCTION acrescimoParaJuros(REAL acrescimo; INTEGER precisao; INTEGER maxIteracoes; REAL maximoJuros)
    REAL pesoTotal = getPesoTotal()
    IF maxIteracoes < 1 OR precisao < 1 OR maximoJuros <= 0.0 OR acrescimo <= 0.0 OR quantidade < 1 OR periodo <= 0.0 OR pesoTotal <= 0.0 THEN
        RETURN 0.0
    END IF

    REAL minJuros = 0.0
    REAL medJuros = maximoJuros/2.0
    REAL maxJuros = maximoJuros
    REAL minDiferenca = 0.1^precisao

    LOOP FOR indice = 1 TO maxIteracoes
        medJuros = (minJuros+maxJuros)/2.0
        IF (maxJuros-minJuros) < minDiferenca THEN
            RETURN medJuros
        END IF
        IF jurosParaAcrescimo(medJuros) < acrescimo THEN
            minJuros = medJuros
        ELSE
            maxJuros = medJuros
        END IF
    REPEAT LOOP

    RETURN medJuros
END acrescimoParaJuros

! potencia = base^expoente

! calcula e guarda os valores das funções
REAL pesoTotal = getPesoTotal()
REAL acrescimoCalculado = jurosParaAcrescimo(3.0)
REAL jurosCalculado = acrescimoParaJuros(acrescimoCalculado, 15, 100, 50.0)

! mostra os resultados
WRITE "Peso total = "+pesoTotal
WRITE "Acréscimo = "+acrescimoCalculado
WRITE "Juros = "+jurosCalculado