// Cálculo dos juros, sendo que precisa de parcelas pra isso
// Versão 0.1: 12/03/2025: versão feita sem muito conhecimento de MEL

// variáveis globais para simplificar as chamadas
global int $Quantidade = 3;
global int $Composto = true;
global float $Periodo = 30.0;
global float $Pagamentos[];
global float $Pesos[];

// calcula a somatória de pesos[]
global proc float getPesoTotal() {
    global int $Quantidade; global float $Pesos[];
    float $acumulador = 0.0; int $indice;
    for($indice = 0; $indice < $Quantidade; $indice++) $acumulador += $Pesos[$indice];
    return $acumulador;
}

// calcula o acréscimo a partir dos juros e parcelas
global proc float jurosParaAcrescimo(float $juros) {
    global int $Quantidade; global int $Composto; global float $Periodo; global float $Pagamentos[]; global float $Pesos[];
    float $pesoTotal = getPesoTotal(); float $acumulador = 0.0; int $indice;
    if(($pesoTotal <= 0.0) || ($Quantidade < 1) || ($Periodo <= 0.0) || ($juros <= 0.0)) return 0.0;
    
    for($indice = 0; $indice < $Quantidade; $indice++)
        if($Composto) $acumulador += $Pesos[$indice] / pow(1.0 + $juros / 100.0, $Pagamentos[$indice] / $Periodo);
            else $acumulador += $Pesos[$indice] / (1.0 + $juros / 100.0 * $Pagamentos[$indice] / $Periodo);
            
    if($acumulador <= 0.0) return 0.0;
    return ($pesoTotal / $acumulador - 1.0) * 100.0;
}

// calcula os juros a partir do acréscimo e parcelas
global proc float acrescimoParaJuros(float $acrescimo, int $precisao, int $maxIteracoes, float $maxJuros) {
    global int $Quantidade; global float $Periodo;
    float $pesoTotal = getPesoTotal(); float $minJuros = 0.0; float $minDiferenca = pow(0.1, $precisao); float $medJuros; int $indice;
    if(($pesoTotal <= 0.0) || ($Quantidade < 1) || ($Periodo <= 0.0) || ($acrescimo <= 0.0) || ($precisao < 1) || ($maxIteracoes < 1) || ($maxJuros <= 0.0)) return 0.0;
    
    for($indice = 0; $indice < $maxIteracoes; $indice++) {
        $medJuros = ($minJuros + $maxJuros) / 2.0;
        if(($maxJuros - $minJuros) < $minDiferenca) return $medJuros;
        if(jurosParaAcrescimo($medJuros) < $acrescimo) $minJuros = $medJuros; else $maxJuros = $medJuros;
    }
    return $medJuros;
}

// preenche os arrays
int $indice;
for($indice = 0; $indice < $Quantidade; $indice++) {
    $Pagamentos[$indice] = 30.0 * ($indice + 1.0);
    $Pesos[$indice] = 1.0;
}

// calcula e guarda os resultados das funções
float $pesoTotal = getPesoTotal();
float $acrescimoCalculado = jurosParaAcrescimo(3.0);
float $jurosCalculado = acrescimoParaJuros($acrescimoCalculado, 15, 100, 50.0);

// imprime os resultados
print("Peso total = " + $pesoTotal + "\n");
print("Acréscimo = " + $acrescimoCalculado + "\n");
print("Juros = " + $jurosCalculado + "\n");