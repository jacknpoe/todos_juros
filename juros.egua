// Calcula o acréscimo a partir dos juros e os juros a partir do acréscimo
// NOTA: para a versão em classes não tem documentação o suficiente; com o que existe, não roda em Delégua;
//       em Légua (on-line), mesmo tirando a declaração de atributos, ela diz em isto.pagamentos que isto não existe.
// Versão: 0.1.1: 15/01/2026: convertido de Delégua

// varíaveis globais e atribuições de valores
var Quantidade = 3;
var Composto = verdadeiro;
var Periodo = 30.0;
var Pagamentos = [];
var Pesos = [];

// inicializa os vetores Pagamentos e Pesos
para(var indice = 0; indice < Quantidade; indice = indice + 1) {
    Pagamentos[indice] = (indice + 1.0) * Periodo;
    Pesos[indice] = 1.0;
}

// calcula a somatória dos elementos do vetor Pesos
função getPesoTotal () {
    var acumulador = 0.0;
    para(var indice = 0; indice < Quantidade; indice = indice + 1) { acumulador = acumulador + Pesos[indice]; }
    retorna acumulador;
}

// calcula o acréscimo a partir dos juros e parcelas
função jurosParaAcrescimo (juros) {
    var pesoTotal = getPesoTotal();
    se (Quantidade < 1 ou Periodo <= 0.0 ou pesoTotal <= 0.0 ou juros <= 0.0) { retorna 0.0; } 
    var acumulador = 0.0;

    para(var indice = 0; indice < Quantidade; indice = indice + 1)  {
        se (Composto) {
            acumulador = acumulador + Pesos[indice] / (1.0 + juros / 100.0) ** (Pagamentos[indice] / Periodo);
        } senão {
            acumulador = acumulador + Pesos[indice] / (1.0 + juros / 100.0 * Pagamentos[indice] / Periodo);
        }
    }

    se (acumulador <= 0.0) { retorna 0.0; }
    retorna (pesoTotal / acumulador - 1.0) * 100.0;
}

// calcula os juros a partir do acréscimo e parcelas
função acrescimoParaJuros (acrescimo, precisao, maxIteracoes, maxJuros) {
    var pesoTotal = getPesoTotal();
    se (Quantidade < 1 ou Periodo <= 0.0 ou pesoTotal <= 0.0 ou acrescimo <= 0.0 ou precisao < 1 ou maxIteracoes < 1 ou maxJuros <= 0.0) { retorna 0.0; } 
    var minJuros = 0.0;
    var medJuros = maxJuros / 2.0;
    var minDiferenca = 0.1 ** precisao;

    para(var indice = 0; indice < maxIteracoes; indice = indice + 1)  {
        se (maxJuros - minJuros <= minDiferenca) { retorna medJuros; }    
        se (jurosParaAcrescimo(medJuros) < acrescimo) {
            minJuros = medJuros;
        } senão {
            maxJuros = medJuros;
        }
        medJuros = (minJuros + maxJuros) / 2.0;
    }

    retorna medJuros;
}

// calcula e guarda os resultados das funções
var pesoTotal = getPesoTotal();
var acrescimoCalculado = jurosParaAcrescimo(3.0);
var jurosCalculado = acrescimoParaJuros(acrescimoCalculado, 15, 65, 50.0);

// imprime os resultados
escreva("Peso total = " + texto(pesoTotal));
escreva("Acréscimo = " + texto(acrescimoCalculado));
escreva("Juros = " + texto(jurosCalculado));
