#!/bin/slash

# Cálculo dos juros, sendo que precisa de parcelas pra isso
# Versão 0.1: 24/04/2025: versão feita sem muito conhecimento de Slash

# variáveis globais para simplificar as chamadas às funções
let Quantidade = 3
let Composto = 1   # 0 = false
let Periodo = 30.0
let Pagamentos = [30.0, 60.0, 90.0]
let Pesos = [1.0, 1.0, 1.0]

# calcula a somatória de Pesos[]
function getPesoTotal() {
    let acumulador = 0.0
    for indice = 0; indice < Quantidade; indice = indice + 1 {
        acumulador = acumulador + Pesos[indice]
    }
    return acumulador
}

# calcula o acréscimo a partir dos juros e parcelas
function jurosParaAcrescimo(juros) {
    let pesoTotal = getPesoTotal()
    if Quantidade < 1 || Periodo < 0.0 || Periodo == 0.0  ||
       pesoTotal < 0.0 || pesoTotal == 0.0 || juros < 0.0 || juros == 0.0 { return 0.0 }

    let acumulador = 0.0
    for indice = 0; indice < Quantidade; indice = indice + 1 {
        if Composto {
            acumulador = acumulador + Pesos[indice] / (1.0 + juros / 100.0) ^ (Pagamentos[indice] / Periodo)
        } else {
            acumulador = acumulador + Pesos[indice] / (1.0 + juros / 100.0 * Pagamentos[indice] / Periodo)
        }
    }

    if acumulador < 0.0 || acumulador == 0.0 { return 0.0 }
    return (pesoTotal / acumulador - 1.0) * 100.0
}

# calcula os juros a partir do acréscimo e parcelas
function acrescimoParaJuros(acrescimo, precisao, maxIteracoes, maxJuros) {
    let pesoTotal = getPesoTotal()
    if Quantidade < 1 || Periodo < 0.0 || Periodo == 0.0  || pesoTotal < 0.0 || pesoTotal == 0.0 ||
       acrescimo < 0.0 || acrescimo == 0.0 || precisao < 1 || maxIteracoes < 1 || maxJuros < 0.0 || maxJuros == 0.0 { return 0.0 }
    
    let minJuros = 0.0
    let medJuros = maxJuros / 2.0
    let minDiferenca = 0.1 ^ precisao
    for indice = 0; indice < maxIteracoes; indice = indice + 1 {
        if maxJuros - minJuros < minDiferenca { return medJuros }
        if jurosParaAcrescimo(medJuros) < acrescimo { minJuros = medJuros } else { maxJuros = medJuros }
        medJuros = (minJuros + maxJuros) / 2.0
    }
    return medJuros
}

# chama e guarda os valores das funções
let pesoTotal = getPesoTotal()
let acrescimoCalculado = jurosParaAcrescimo(3.0)
let jurosCalculado = acrescimoParaJuros(acrescimoCalculado, 15, 100, 50.0)

# imprime os resultados
println("Peso total = " + to_str(pesoTotal))
println("Acréscimo = " + to_str(acrescimoCalculado))
println("Juros = " + to_str(jurosCalculado))