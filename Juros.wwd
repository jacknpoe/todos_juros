'#Language "WWB-COM"
' Cálculo dos juros, sendo que precisa de parcelas pra isso
' Versão 0.1: 30/03/2025: versão feita sem muito conhecimento de WWB-COM
'        0.2: 01/04/2025: alterados os ReDims para Quantidade - 1

Option Explicit

' variáveis globais para simplificar as chamadas
Dim Quantidade As Integer
Dim Composto As Boolean
Dim Periodo As Double
Dim Pagamentos() As Double
Dim Pesos() As Double

' calcula a somatória de Pesos()
Function getPesoTotal As Double
	Dim acumulador As Double
	Dim indice As Integer
	For indice = 0 To Quantidade - 1
		acumulador += Pesos(indice)
	Next
	Return acumulador
End Function

' calcula o acréscimo a partir dos juros e parcelas
Function jurosParaAcrescimo (juros As Double) As Double
	Dim pesoTotal, acumulador As Double
	Dim indice As Integer
	pesoTotal = getPesoTotal
	If Quantidade < 1 OrElse Periodo <= 0.0 OrElse juros <= 0.0 OrElse pesoTotal <= 0.0 Then Return 0.0

	For indice = 0 To Quantidade - 1
		If Composto Then
			acumulador += Pesos(indice) / (1.0 + juros / 100.0) ^ (Pagamentos(indice) / Periodo)
		Else
			acumulador += Pesos(indice) / (1.0 + juros / 100.0 * Pagamentos(indice) / Periodo)
		End If
	Next indice

	If acumulador <= 0.0 Then Return 0.0
	Return (pesoTotal / acumulador - 1.0) * 100.0
End Function

' calcula os juros a partir do acréscimo e parcelas
Function acrescimoParaJuros (acrescimo As Double, precisao As Integer, maxIteracoes As Integer, ByVal maxJuros As Double) As Double
	Dim pesoTotal, minJuros, medJuros, minDiferenca As Double
	Dim indice As Integer
	pesoTotal = getPesoTotal
	If Quantidade < 1 OrElse Periodo <= 0.0 OrElse acrescimo <= 0.0 OrElse pesoTotal <= 0.0 OrElse precisao < 1 OrElse maxIteracoes < 1 OrElse maxJuros <= 0.0 Then Return 0.0

	medJuros = maxJuros / 2.0
	minDiferenca = 0.1 ^ precisao
	For indice = 1 To maxIteracoes
		If maxJuros - minJuros < minDiferenca Then Return medJuros
		If jurosParaAcrescimo(medJuros) < acrescimo Then minJuros = medJuros Else maxJuros = medJuros
		medJuros = (minJuros + maxJuros) / 2.0
	Next indice
	Return medJuros
End Function

'	Potencia = Valor ^ Expoente

Sub Main
	Dim indice As Integer
	Dim pesoTotal, acrescimoCalculado, jurosCalculado As Double

	' inicializa as variáveis globais
	Quantidade = 3
	Composto = True
	Periodo = 30.0
	ReDim Pagamentos(Quantidade - 1) As Double
	ReDim Pesos(Quantidade - 1) As Double

	For indice = 0 To Quantidade - 1
		Pagamentos(indice) = (indice + 1.0) * Periodo
		Pesos(indice) = 1.0
	Next indice

	' calcula e guarda os resultados das funções
	pesoTotal = getPesoTotal
	acrescimoCalculado = jurosParaAcrescimo(3.0)
	jurosCalculado = acrescimoParaJuros(acrescimoCalculado, 15, 100, 50.0)

	' grava os resultados no arquivo "Juros.txt"
    Open "Juros.txt" For Output As #1
	Print #1, "Peso total ="; pesoTotal
	Print #1, "Acréscimo ="; acrescimoCalculado
	Print #1, "Juros ="; jurosCalculado
    Close #1
End Sub
