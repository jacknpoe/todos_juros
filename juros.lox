// Cálculo dos juros, sendo que precisa de parcelas pra isso
// Versão 0.1: 08/02/2026: versao feita sem muito conhecimento de lox (jlox Ahmad-Faraj implementation)

// devem ser vistas como constantes de precisão das funções ln() e exp()
var MAXLN = 20;
var MAXEXP = 30;

// as variáveis globais estão aqui por uma questão de organização, não de necessidade, para simplpificar as chamadas de função
var Quantidade = 3;
var Composto = true;
var Periodo = 30.0;

// função que substitui o array Pagamentos[] porque lox não tem arrays
fun Pagamentos(indice) { return (indice + 1.0) * Periodo; }

// função que substitui o array Pesos[] porque lox não tem arrays
fun Pesos(indice) { return 1.0; }

// essa função é mais precisa com valores mais próximos de 1, como 1.0 a 1.1, que é a faixa em que vai ser usada
fun ln(valor) {
  var ip = (valor - 1.0) / (valor + 1.0);
  var termo = ip;
  var soma = 0.0;

  for(var indice = 1; indice <= MAXLN; indice = indice + 1) {
    soma = soma + termo / (2.0 * indice - 1.0);
    termo = termo * ip * ip;
  }

  return 2.0 * soma;
}

// essa função é mais precisa com valores menores do que 5, que é a faixa em que vai ser usada
fun exp(valor) {
  var termo = 1.0;
  var soma = 1.0;

  for(var indice = 1; indice <= MAXEXP; indice = indice + 1) {
    termo = termo * valor / indice;
    soma = soma + termo;
  }

  return soma;
}

// essa função tem a precisão boa de acordo com ln() e exp()
fun pow(base, expoente) {
  return exp(ln(base) * expoente);
}

// função para fazer 0.1 ^ precisao
fun powint(base, expoente) {
  var produto = 1.0;
  for(var indice = 0; indice < expoente; indice = indice + 1) produto = produto * base;
  return produto;
}

// calcula a somatória dos "elementos" em Pesos[]
fun getPesoTotal() {
  var acumulador = 0.0;
  for(var indice = 0; indice < Quantidade; indice = indice + 1) acumulador = acumulador + Pesos(indice);
  return acumulador;
}

// calcula o acréscimo a partir de juros e parcelas
fun jurosParaAcrescimo(juros) {
  var pesoTotal = getPesoTotal();
  if(Quantidade < 1 or Periodo <= 0.0 or pesoTotal <= 0.0 or juros <= 0.0) return 0.0;
  var acumulador = 0.0;

  for(var indice = 0; indice < Quantidade; indice = indice + 1) {
    if(Composto) {
      acumulador = acumulador + Pesos(indice) / pow(1.0 + juros / 100.0, Pagamentos(indice) / Periodo);
    } else {
      acumulador = acumulador + Pesos(indice) / (1.0 + juros / 100.0 * Pagamentos(indice) / Periodo);
    }
  }

  if(acumulador <= 0.0) return 0.0;
  return (pesoTotal / acumulador - 1.0) * 100.0;
}

// calcula os juros a partir do acréscimo e parcelas
fun acrescimoParaJuros(acrescimo, precisao, maxIteracoes, maxJuros) {
  var pesoTotal = getPesoTotal();
  if(Quantidade < 1 or Periodo <= 0.0 or pesoTotal <= 0.0 or acrescimo <= 0.0 or precisao < 1 or maxIteracoes < 1 or maxJuros <= 0.0) return 0.0;
  var minJuros = 0.0;
  var medJuros = maxJuros / 2.0;
  var minDiferenca = powint(0.1, precisao);
  
  for(var iteracao = 0; iteracao < maxIteracoes; iteracao = iteracao + 1) {
    if(maxJuros - minJuros < minDiferenca) return medJuros;
    if(jurosParaAcrescimo(medJuros) < acrescimo) {
      minJuros = medJuros;
    } else {
      maxJuros = medJuros;
    }
    medJuros = (minJuros + maxJuros) / 2.0;
  }

  return medJuros;
}

// chama e guarda os resultados das funções
var pesoTotal = getPesoTotal();
var acrescimoCalculado = jurosParaAcrescimo(3.0);
var jurosCalculado = acrescimoParaJuros(acrescimoCalculado, 15, 65, 50.0);

// imprime os resultados
print "Peso total ="; print pesoTotal;
print "Acréscimo ="; print acrescimoCalculado;
print "Juros ="; print jurosCalculado;
