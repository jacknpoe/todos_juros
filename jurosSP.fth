\ Cálculo dos juros, sendo que precisa de parcelas pra isso
\ Versão 0.1: 03/01/2026: versão adaptada de pForth, feita sem muito conhecimento de SP-Forth

REQUIRE F. lib/include/float2.f  \ para operações com floats

\ variáveis globais para simnplificar as chamadas, definições e inicializações
VARIABLE QUANTIDADE
3 QUANTIDADE !
VARIABLE COMPOSTO
-1 COMPOSTO !       \ -1 = TRUE
FVARIABLE PERIODO
30.0e PERIODO F!
CREATE PAGAMENTOS QUANTIDADE @ FLOATS ALLOT
CREATE PESOS QUANTIDADE @ FLOATS ALLOT

\ inicializa os arrays
FVARIABLE IAINDICE
: INITARRAYS ( -- )
    1.0e IAINDICE F!
	QUANTIDADE @ 0 DO
        \ I S>F 1.0e F+ PERIODO F@ F* PAGAMENTOS I FLOATS + F!        
		IAINDICE F@ PERIODO F@ F* PAGAMENTOS I FLOATS + F!
		1.0e PESOS I FLOATS + F!
        1.0e IAINDICE F@ F+ IAINDICE F!
	LOOP
;

\ calcula a somatória de PESOS
FVARIABLE GPTACUMULADOR
: GETPESOTOTAL ( -- TOTAL )
	0.0e GPTACUMULADOR F!
	QUANTIDADE @ 0 DO
		PESOS I FLOATS + F@ GPTACUMULADOR F@ F+ GPTACUMULADOR F!
	LOOP
	GPTACUMULADOR F@
;

\ calcula o acréscimo a partir dos juros e parcelas
FVARIABLE JPAACUMULADOR
FVARIABLE JPAPESOTOTAL
FVARIABLE JPAJUROS
: JUROSPARAACRESCIMO ( F: JUROS -- ACRESCIMO )
    JPAJUROS F!
    GETPESOTOTAL JPAPESOTOTAL F!
    QUANTIDADE @ 1 < PERIODO F@ 0.0e F< JPAJUROS F@ 0.0e F< JPAPESOTOTAL F@ 0.0e F< OR OR OR IF
        0.0e
    ELSE
        0.0e JPAACUMULADOR F!
        QUANTIDADE @ 0 DO
            COMPOSTO @ IF
                PESOS I FLOATS + F@ 1.0e JPAJUROS F@ 100.0e F/ F+ PAGAMENTOS I FLOATS + F@ PERIODO F@ F/ F** F/ JPAACUMULADOR F@ F+ JPAACUMULADOR F!
            ELSE
                PESOS I FLOATS + F@ 1.0e JPAJUROS F@ 100.0e F/ PAGAMENTOS I FLOATS + F@ F* PERIODO F@ F/ F+ F/ JPAACUMULADOR F@ F+ JPAACUMULADOR F!
            THEN
        LOOP
        JPAACUMULADOR F@ 0.0e F< IF
            0.0e
        ELSE
            JPAPESOTOTAL F@ JPAACUMULADOR F@ F/ 1.0e F- 100.0e F*
        THEN
    THEN
;

\ calcula os juros a partir do acréscimo e parcelas
FVARIABLE PJAPESOTOTAL
FVARIABLE PJAACRESCIMO
FVARIABLE PJAMINJUROS
FVARIABLE PJAMEDJUROS
FVARIABLE PJAMAXJUROS
VARIABLE  PJAMAXITERACOES
FVARIABLE  PJAPRECISAO
FVARIABLE PJAMINDIFERENCA
VARIABLE  PJAITERACAO
: ACRESCIMOPARAJUROS ( F: ACRESCIMO PRECISAO MAXJUROS  D: MAXITERACOES -- JUROS )
    PJAMAXITERACOES !
    PJAMAXJUROS F!
    PJAPRECISAO F!
    PJAACRESCIMO F!
    GETPESOTOTAL PJAPESOTOTAL F!
    QUANTIDADE @ 1 < PERIODO F@ 0.0e F< PJAACRESCIMO F@ 0.0e F< PJAPRECISAO F@ 1.0e F< PJAMAXITERACOES @ 1 < PJAMAXJUROS F@ 0.0e F< PJAPESOTOTAL F@ 0.0e F< OR OR OR OR OR OR
    IF
        0.0e
    ELSE
        0.0e PJAMINJUROS F!
        PJAMAXJUROS F@ 2.0e F/ PJAMEDJUROS F!
        0.1e PJAPRECISAO F@ F** PJAMINDIFERENCA F!
        0 PJAITERACAO !
        BEGIN
            PJAITERACAO @ PJAMAXITERACOES @ <
        WHILE
            PJAMAXJUROS F@ PJAMINJUROS F@ F- PJAMINDIFERENCA F@ F< IF
                PJAMAXITERACOES @ PJAITERACAO !
            ELSE
                PJAMEDJUROS F@ JUROSPARAACRESCIMO PJAACRESCIMO F@ F< IF
                    PJAMEDJUROS F@ PJAMINJUROS F!
                ELSE
                    PJAMEDJUROS F@ PJAMAXJUROS F!
                THEN
                PJAMINJUROS F@ PJAMAXJUROS F@ F+ 2.0e F/ PJAMEDJUROS F!
                1 PJAITERACAO +!
            THEN
        REPEAT

        PJAMEDJUROS F@
    THEN
;

\ inicializa os arrays globais
INITARRAYS

\ calcula e guarda os resultados das funções
FVARIABLE PESOTOTAL
FVARIABLE ACRESCIMOCALCULADO
FVARIABLE JUROSCALCULADO
GETPESOTOTAL PESOTOTAL F!
3.0e JUROSPARAACRESCIMO ACRESCIMOCALCULADO F!
ACRESCIMOCALCULADO F@ 50.0e 15.0e 100 ACRESCIMOPARAJUROS JUROSCALCULADO F!

\ imprime os resultados
16 SET-PRECISION
.( Peso total = ) PESOTOTAL F@ F. CR
.( Acrescimo = ) ACRESCIMOCALCULADO F@ F. CR
.( Juros = ) JUROSCALCULADO F@ F. CR

BYE  \ fecha o SP-Forth
