00003 REM Calculo dos juros, sendo que precisa de arrays para isso
00006 REM Versao 0.1: 09/03/2025: feita sem muito conhecimento de ZBasic
00095 REM variaveis globais e atribuicao de valores
00100 QUANTIDADE% = 3
00110 COMPOSTO% = 1
00120 PERIODO# = 30#
00130 DIM PAGAMENTOS#(2)
00140 DIM PESOS#(2)
00200 FOR INDICE% = 0 TO QUANTIDADE% - 1
00210 PAGAMENTOS#(INDICE%) = 30# * (INDICE% + 1#)
00220 PESOS#(INDICE%) = 1#
00230 NEXT INDICE%
00295 REM testa os retornos das funcoes
00300 GOSUB 500
00310 PRINT "Peso total ="; PESOTOTAL#
00320 JUROS# = 3#
00330 GOSUB 1000
00340 PRINT "Acrescimo ="; ACRESCIMO#
00350 PRECISAO% = 40
00360 MAXITERACOES% = 300
00370 MAXJUROS# = 50#
00380 ACRESCIMOPARAM# = ACRESCIMO#
00390 GOSUB 1500
00400 PRINT "Juros ="; JUROS#
00450 END
00495 REM calcula a somatoria dos pesos
00500 PESOTOTAL# = 0#
00510 FOR INDICE% = 0 TO QUANTIDADE% - 1
00520 PESOTOTAL# = PESOTOTAL# + PESOS#(INDICE%)
00530 NEXT INDICE%
00540 RETURN
00995 REM calcula o acrescimo a partir dos juros e parcelas
01000 ACRESCIMO# = 0#
01010 IF JUROS# <= 0# THEN RETURN
01020 IF QUANTIDADE% < 1 THEN RETURN
01030 IF PERIODO# <= 0# THEN RETURN
01040 GOSUB 500
01050 IF PESOTOTAL# <= 0# THEN RETURN
01060 ACUMULADOR# = 0#
01070 FOR INDICE% = 0 TO QUANTIDADE% - 1
01080 IF COMPOSTO% = 1 THEN GOTO 1100 ELSE GOTO 1200
01100 ACUMULADOR# = ACUMULADOR# + PESOS#(INDICE%) / (1# + JUROS# / 100#) ^ (PAGAMENTOS#(INDICE%) / PERIODO#)
01110 GOTO 1300
01200 TEMP# = PESOS#(INDICE%) \ (1# + ((JUROS# \ 100#) * PAGAMENTOS#(INDICE%) \ PERIODO#))
01210 ACUMULADOR# = ACUMULADOR# + TEMP#
01300 NEXT INDICE%
01310 ACRESCIMO# = (PESOTOTAL# / ACUMULADOR# - 1#) * 100#
01320 RETURN
01495 REM calcula os juros a partir do acrescimo e parcelas
01500 JUROS# = 0#
01510 IF MAXITERACOES% < 1 THEN RETURN
01520 IF QUANTIDADE% < 1 THEN RETURN
01530 IF PRECISAO% < 1 THEN RETURN
01540 IF PERIODO# <= 0# THEN RETURN
01550 IF ACRESCIMOPARAM# <= 0# THEN RETURN
01560 IF MAXJUROS# <= 0# THEN RETURN
01570 GOSUB 500
01580 IF PESOTOTAL# <= 0# THEN RETURN
01590 MINJUROS# = 0#
01610 MINDIFERENCA# = 0.1 ^ PRECISAO%
01620 FOR ITERACAO% = 1 TO MAXITERACOES%
01630 JUROS# = (MINJUROS# + MAXJUROS#) / 2#
01640 IF (MAXJUROS# - MINJUROS#) < MINDIFERENCA# THEN RETURN
01650 GOSUB 1000
01660 IF ACRESCIMO# < ACRESCIMOPARAM# THEN MINJUROS# = JUROS# ELSE MAXJUROS# = JUROS#
01670 NEXT ITERACAO%
01680 RETURN
