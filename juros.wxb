' Cálculo dos juros, sendo que precisa de parcelas pra isso
' Versão 0.1: 04/02/2026: versão feita sem muito conhecimento de wxBasic

' variáveis globais para simplificar as chamadas às funções e inicialização das escalares
Quantidade = 3
Composto = True
Periodo = 30.0
Dim Pagamentos[Quantidade]
Dim Pesos[Quantidade]

' inicialização dos elementos dos arrrays
For indice = 1 To Quantidade
	Pagamentos[indice] = indice * Periodo
	Pesos[indice] = 1.0
Next

' calcula e guarda os retornos das funções
pesoTotal = getPesoTotal()
acrescimoCalculado = jurosParaAcrescimo(3.0)
jurosCalculado = acrescimoParaJuros(acrescimoCalculado, 6, 26, 50.0)

' imprime os resultados
Print "Peso total = "; pesoTotal
Print "Acréscimo = "; acrescimoCalculado
Print "Juros = "; jurosCalculado

' calcula a somatória dos elementos do array Pesos[]
Function getPesoTotal()
	acumulador = 0.0
	For indice = 1 To Quantidade
		acumulador += Pesos[indice]
	Next
	Return acumulador
End Function

' calcula o acréscimo a partir dos juros e parcelas
Function jurosParaAcrescimo(juros)
	pesoTotal = getPesoTotal()
	If Quantidade < 1 Or Periodo <= 0.0 Or pesoTotal <= 0.0 Or juros <= 0.0 Then
		Return 0.0
	End If
	acumulador = 0.0
	
	For indice = 1 To Quantidade
		If Composto Then
			acumulador += Pesos[indice] / (1.0 + juros / 100.0) ^ (Pagamentos[indice] / Periodo)
		Else
			acumulador += Pesos[indice] / (1.0 + (juros / 100.0 * Pagamentos[indice] / Periodo))
		End If
	Next
	
	If acumulador <= 0.0 Then
		Return 0.0
	End IF
	Return (pesoTotal / acumulador - 1.0) * 100.0
End Function

' calcula os juros a partir do acréscimo e parcelas
Function acrescimoParaJuros(acrescimo, precisao, maxIteracoes, maxJuros)
	pesoTotal = getPesoTotal()
	If Quantidade < 1 Or Periodo <= 0.0 Or pesoTotal <= 0.0 Or acrescimo <= 0.0 Or precisao < 1 Or maxIteracoes < 1 Or maxJuros <= 0.0 Then
		Return 0.0
	End If
	minJuros = 0.0
	medJuros = maxJuros / 2.0
	minDiferenca = 0.1 ^ precisao
	
	For iteracao = 1 to maxIteracoes
		If maxJuros - minJuros < minDiferenca Then
			Return medJuros
		End If
		If jurosParaAcrescimo(medJuros) < acrescimo Then
			minJuros = medJuros
		Else
			maxJuros = medJuros
		End If
		medJuros = (minJuros + maxJuros) / 2.0
	Next
	
	Return medJuros
End Function
