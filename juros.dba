` Cálculo dos juros, sendo que precisa de parcelas pra isso
` Versão 0.1: 11/04/2025: versão feita sem muito conhecimento de DarkBASIC

` variáveis globais para simplificar as chamadas
global Quantidade = 3
global Composto = 1  ` 1 = true, outro = false
global Periodo# = 30.0
global dim Pagamentos#(Quantidade)
global dim Pesos#(Quantidade)

` os arrays são inicializados dinamicamente
for indice = 0 to Quantidade - 1
  Pagamentos#(indice) = (indice + 1.0) * Periodo#
  Pesos#(indice) = 1.0
next indice

` calcula e guarda os resultados das funções
pesoTotal# = getPesoTotal()
acrescimoCalculado# = jurosParaAcrescimo(3.0)
jurosCalculado# = acrescimoParaJuros(acrescimoCalculado#, 15, 100, 50.0)

` imprime os resultados
print "Peso total = "; pesoTotal#
print "Acréscimo = "; acrescimoCalculado#
print "Juros = "; jurosCalculado#
wait key
end

` calcula os juros a partir do acréscimo e parcelas
function acrescimoParaJuros(acrescimo#, precisao, maxIteracoes, maxJuros#)
  pesoTotal# = getPesoTotal()
  if Quantidade < 1 or Periodo# <= 0.0 or pesoTotal# <= 0.0 or acrescimo# <= 0.0 or precisao < 1 or maxIteracoes < 1 or maxJuros# <= 0.0
    medJuros# = 0.0
  else
    minJuros# = 0.0
    medJuros# = maxJuros# / 2.0
    minDiferenca# = 0.1 ^ precisao
    indice = 0
    while indice < maxIteracoes
      if maxJuros# - minJuros# < minDiferenca# then indice = maxIteracoes
      if jurosParaAcrescimo(medJuros#) < acrescimo#
        minJuros# = medJuros#
      else
        maxJuros# = medJuros#
      endif
      medJuros# = (minJuros# + maxJuros#) / 2.0
      indice = indice + 1
    endwhile
  endif
endfunction medJuros#

` calcula o acréscimo a partir dos juros e parcelas
function jurosParaAcrescimo(juros#)
  pesoTotal# = getPesoTotal()
  if Quantidade < 1 or Periodo# <= 0.0 or pesoTotal# <= 0.0 or juros# <= 0.0
    resultado# = 0.0
  else
    acumulador# = 0.0
    for indice = 0 to Quantidade - 1
      if Composto = 1
        acumulador# = acumulador# + Pesos#(indice) / (1.0 + juros# / 100.0) ^ (Pagamentos#(indice) / Periodo#)
      else
        acumulador# = acumulador# + Pesos#(indice) / (1.0 + juros# / 100.0 * Pagamentos#(indice) / Periodo#)
      endif
    next indice
    if acumulador# <= 0.0
      resultado# = 0.0
    else
      resultado# = (pesoTotal# / acumulador# - 1.0) * 100.0
    endif
  endif
endfunction resultado#

` calcula a somatória de Pesos()
function getPesoTotal()
  acumulador# = 0.0
  for indice = 0 to Quantidade - 1
    acumulador# = acumulador# + Pesos#(indice)
  next indice
endfunction acumulador#
